__author__ = "Jessika Nordin, Padraic Corcoran, Arielle R Munters"
__copyright__ = "Copyright 2022"
__email__ = "jessika.nordin@scilifelab.uu.se"
__license__ = "GPL-3"


include: "rules/common.smk"
include: "rules/fix_sv_header.smk"
include: "rules/bcftools.smk"
include: "rules/vcf_to_aed.smk"
include: "rules/add_ref_to_vcf.smk"
include: "rules/concat_vcfs.smk"
include: "rules/coverage.smk"
include: "rules/extract_str_bed.smk"
include: "rules/peddy.smk"


report: "report/workflow.rst"


rule all:
    input:
        unpack(compile_output_list),


ruleorder: _copy_samtools_crai > compression_samtools_index
ruleorder: _copy_snv_indels_tbi > misc_tabix
ruleorder: _copy_snv_indels_filtered_tbi > misc_tabix
ruleorder: parabricks_fq2bam > alignment_samtools_index
ruleorder: snv_indels_glnexus_trio > snv_indels_glnexus
ruleorder: snv_indels_tabix > misc_tabix


module alignment:
    snakefile:
        get_module_snakefile(config, "hydra-genetics/alignment", path="workflow/Snakefile", tag=config["modules"]["alignment"])
    config:
        config


use rule * from alignment as alignment_*


use rule samtools_sort from alignment as alignment_samtools_sort_eh_bam with:
    input:
        bam="cnv_sv/expansionhunter/{sample}_{type}_realigned.bam",
    output:
        bam=temp("cnv_sv/expansionhunter/{sample}_{type}_realigned.sorted.bam"),
    log:
        "cnv_sv/expansionhunter/{sample}_{type}_realigned.sorted.bam.log",
    benchmark:
        repeat(
            "cnv_sv/expansionhunter/{sample}_{type}_realigned.sorted.bam.benchmark.tsv",
            config.get("samtools_sort", {}).get("benchmark_repeats", 1),
        )


module annotation:
    snakefile:
        get_module_snakefile(config, "hydra-genetics/annotation", path="workflow/Snakefile", tag=config["modules"]["annotation"])
    config:
        config


use rule stranger from annotation as annotation_stranger


use rule vep from annotation as annotation_vep with:
    input:
        cache=config.get("vep", {}).get("vep_cache", ""),
        fasta=config["reference"]["fasta"],
        tabix="vcf_final/{sample}_{type}.bcftools_view_pass.vcf.gz.tbi",
        vcf="vcf_final/{sample}_{type}.bcftools_view_pass.vcf.gz",
    output:
        vcf=temp("vcf_final/{sample}_{type}.vep_annotated.vcf"),
    log:
        "vcf_final/{sample}_{type}.vep_annotated.vcf.log",
    benchmark:
        repeat("vcf_final/{sample}_{type}.vep_annotated.vcf.benchmark.tsv", config.get("vep", {}).get("benchmark_repeats", 1))


use rule vep from annotation as annotation_vep_trio with:
    input:
        cache=config.get("vep_trio", {}).get("vep_cache", ""),
        fasta=config["reference"]["fasta"],
        vcf="snv_indels/glnexus/{sample}_{type}.decomposed.vcf.gz",
    output:
        vcf=temp("snv_indels/glnexus/{sample}_{type}.vep_annotated.vcf"),
    params:
        extra=config.get("vep_trio", {}).get("extra", "--pick"),
        mode=config.get("vep_trio", {}).get("mode", "--offline --cache"),
    log:
        "snv_indels/glnexus/{sample}_{type}.vep_annotated.vcf.log",
    benchmark:
        repeat(
            "snv_indels/glnexus/{sample}_{type}.vep_annotated.vcf.benchmark.tsv",
            config.get("vep", {}).get("benchmark_repeats", 1),
        )


use rule vep from annotation as annotation_vep_svdb with:
    input:
        cache=config.get("vep_svdb", {}).get("vep_cache", ""),
        fasta=config["reference"]["fasta"],
        vcf="cnv_sv/svdb_query/{sample}_{type}.merged.svdb_query_ref.vcf.gz",
    output:
        vcf=temp("annotate/vep_svdb/{sample}_{type}.merged.svdb_query.vep_annotated.vcf"),
    params:
        extra=config.get("vep_svdb", {}).get("extra", "--pick"),
        mode=config.get("vep_svdb", {}).get("mode", "--offline --cache"),
    log:
        "annotate/vep_svdb/{sample}_{type}.merged.svdb_query.vep_annotated.vcf.log",
    benchmark:
        repeat(
            "annotate/vep_svdb/{sample}_{type}.merged.svdb_query.vep_annotated.vcf.benchmark.tsv",
            config.get("vep_svdb", {}).get("benchmark_repeats", 1),
        )


module cnv_sv:
    snakefile:
        get_module_snakefile(config, "hydra-genetics/cnv_sv", path="workflow/Snakefile", tag=config["modules"]["cnv_sv"])
    config:
        config


use rule * from cnv_sv as cnv_sv_*


use rule automap from cnv_sv as cnv_sv_automap with:
    input:
        vcf="vcf_final/{sample}_{type}.vcf",


use rule cnvpytor_filter from cnv_sv as cnv_sv_cnvpytor_filter


use rule cnvpytor_readdepth from cnv_sv as cnv_sv_cnvpytor_readdepth with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bai=lambda wildcards: get_bam_input(wildcards)[1],
        vcf=lambda wildcards: get_vcf_input(wildcards),
    params:
        extra=config.get("cnvpytor_readdepth", {}).get("extra", ""),
        length=config.get("cnvpytor_readdepth", {}).get("length_list", ""),
        model=config.get("cnvpytor_readdepth", {}).get("calling_model", ""),


use rule expansionhunter from cnv_sv as cnv_sv_expansionhunter with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bai=lambda wildcards: get_bam_input(wildcards)[1],
        cat=config.get("expansionhunter", {}).get("variant_catalog", ""),
        ref=config.get("reference", {}).get("fasta", ""),
        sex="qc/peddy/peddy.sex_check.csv",


use rule manta_config_n from cnv_sv as cnv_sv_manta_config_n with:
    input:
        bam_n=lambda wildcards: get_bam_input(wildcards, use_type_wildcard=False)[0],
        bai_n=lambda wildcards: get_bam_input(wildcards, use_type_wildcard=False)[1],
        ref=config["reference"]["fasta"],


use rule manta_run_workflow_n from cnv_sv as cnv_sv_manta_run_workflow_n with:
    input:
        bam_n=lambda wildcards: get_bam_input(wildcards, use_type_wildcard=False)[0],
        bai_n=lambda wildcards: get_bam_input(wildcards, use_type_wildcard=False)[1],
        ref=config["reference"]["fasta"],
        scrpt="cnv_sv/manta_run_workflow_n/{sample}/runWorkflow.py",
    output:
        cand_si_vcf=temp("cnv_sv/manta_run_workflow_n/{sample}/results/variants/candidateSmallIndels.vcf.gz"),
        cand_si_tbi=temp("cnv_sv/manta_run_workflow_n/{sample}/results/variants/candidateSmallIndels.vcf.gz.tbi"),
        cand_sv_vcf=temp("cnv_sv/manta_run_workflow_n/{sample}/results/variants/candidateSV.vcf.gz"),
        cand_sv_tbi=temp("cnv_sv/manta_run_workflow_n/{sample}/results/variants/candidateSV.vcf.gz.tbi"),
        dip_sv_vcf=temp("cnv_sv/manta_run_workflow_n/{sample}/results/variants/diploidSV.vcf.gz"),
        dip_sv_tbi=temp("cnv_sv/manta_run_workflow_n/{sample}/results/variants/diploidSV.vcf.gz.tbi"),
        wrk_dir=temp(directory("cnv_sv/manta_run_workflow_n/{sample}/workspace")),


use rule smn_manifest from cnv_sv as cnv_sv_smn_manifest with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bai=lambda wildcards: get_bam_input(wildcards)[1],


use rule svdb_merge from cnv_sv as cnv_sv_svdb_merge with:
    input:
        tiddit="cnv_sv/tiddit/{sample}_{type}.vcf",
        manta="cnv_sv/manta_run_workflow_n/{sample}/results/variants/diploidSV.vcf.gz",
        cnvpytor="cnv_sv/cnvpytor/{sample}_{type}.softfiltered.vcf",
    output:
        vcf=temp("cnv_sv/svdb_merge/{sample}_{type}.merged.vcf"),
    params:
        extra=config.get("svdb_merge", {}).get("extra", ""),
        overlap=config.get("svdb_merge", {}).get("overlap", 0.6),
        bnd_distance=config.get("svdb_merge", {}).get("bnd_distance", 10000),
        priority=config.get("svdb_merge", {}).get("priority", ""),
        vcfs=lambda wildards, input: get_vcfs_for_svdb_merge(wildards, input),
    log:
        "cnv_sv/svdb_merge/{sample}_{type}.merged.vcf.log",
    benchmark:
        repeat(
            "cnv_sv/svdb_merge/{sample}_{type}.merged.benchmark.tsv",
            config.get("svdb_merge", {}).get("benchmark_repeats", 1),
        )


use rule svdb_query from cnv_sv as cnv_sv_svdb_query with:
    input:
        vcf="cnv_sv/svdb_merge/{sample}_{type}.merged.vcf",
    output:
        vcf=temp("cnv_sv/svdb_query/{sample}_{type}.merged.svdb_query.vcf"),
    log:
        "cnv_sv/svdb_query/{sample}_{type}.svdb_query.log",
    benchmark:
        repeat(
            "cnv_sv/svdb_query/{sample}_{type}.svdb_query.benchmark.tsv",
            config.get("svdb_query", {}).get("benchmark_repeats", 1),
        )


use rule tiddit from cnv_sv as cnv_sv_tiddit with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        ref=config["reference"]["fasta"],


module compression:
    snakefile:
        get_module_snakefile(
            config, "hydra-genetics/compression", path="workflow/Snakefile", tag=config["modules"]["compression"]
        )
    config:
        config


use rule * from compression as compression_*


use rule samtools_view from compression as compression_samtools_view with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards, use_sample_wildcard=False)[0],
        bai=lambda wildcards: get_bam_input(wildcards, use_sample_wildcard=False)[1],
        ref=config.get("reference", {}).get("fasta", ""),


module filtering:
    snakefile:
        get_module_snakefile(config, "hydra-genetics/filtering", path="workflow/Snakefile", tag=config["modules"]["filtering"])
    config:
        config


use rule * from filtering as filtering_*


use rule bcftools_view from filtering as filtering_bcftools_view_pass with:
    output:
        vcf=temp("{file}.bcftools_view_pass.vcf.gz"),
    params:
        extra=config.get("bcftools_view_pass", {}).get("extra", ""),


use rule bcftools_filter_exclude_region from filtering as filtering_bcftools_filter_cnvpytor with:
    input:
        vcf="cnv_sv/cnvpytor/{sample}_{type}.vcf",
    output:
        vcf="cnv_sv/cnvpytor/{sample}_{type}.softfiltered.vcf",
    params:
        filter=lambda wildcards: "-e {}".format(config["bcftools_filter_cnvpytor"]["exclude"]),
        extra=config.get("bcftools_filter_cnvpytor", {}).get("extra", ""),
    log:
        "cnv_sv/cnvpytor/{sample}_{type}.softfilered.vcf.log",
    benchmark:
        repeat(
            "cnv_sv/cnvpytor/{sample}_{type}.softfilered.vcf.benchmark.tsv",
            config.get("bcftools_filter_cnvpytor", {}).get("benchmark_repeats", 1),
        )


use rule bcftools_view from filtering as filtering_bcftools_view_svdb with:
    input:
        vcf="annotate/vep_svdb/{sample}_{type}.merged.svdb_query.vep_annotated.vep_info.vcf",
    output:
        vcf="annotate/vep_svdb/{sample}_{type}.merged.svdb_query.vep_annotated.filtered.vcf.gz",
    params:
        extra=config.get("bcftools_view_svdb", {}).get("extra", ""),
    log:
        "cnv_sv/svdb_query/{sample}_{type}.merged.svdb_query_ref.filtered.vcf.log",
    benchmark:
        repeat(
            "cnv_sv/svdb_query/{sample}_{type}.merged.svdb_query_ref.filtered.vcf.benchmark.tsv",
            config.get("bcftools_view_svdb", {}).get("benchmark_repeats", 1),
        )

module misc:
    snakefile:
        get_module_snakefile(
            config, "hydra-genetics/misc", path="workflow/Snakefile", tag=config["modules"]["misc"]
        )
    config:
        config

use rule samtools_index from misc as misc_samtools_index with:
    input:
        bam="cnv_sv/expansionhunter/{sample}_{type}_realigned.sorted.bam",
    output:
        bam="cnv_sv/expansionhunter/{sample}_{type}_realigned.sorted.bam.bai",
    log:
        "{sample}_{type}_realigned.sorted.bam.bai.log",
    benchmark:
        repeat(
            "{sample}_{type}_realigned.sorted.bam.bai.benchmark.tsv",
            config.get("samtools_index", {}).get("benchmark_repeats", 1),
        )

use rule bgzip from misc as misc_bgzip
    
use rule tabix from misc as misc_tabix

module mitochondrial:
    snakefile:
        get_module_snakefile(
            config, "hydra-genetics/mitochondrial", path="workflow/Snakefile", tag=config["modules"]["mitochondrial"]
        )
    config:
        config


use rule * from mitochondrial as mitochondrial_*


use rule gatk_print_reads from mitochondrial as mitochondrial_gatk_print_reads with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bai=lambda wildcards: get_bam_input(wildcards)[1],


module parabricks:
    snakefile:
        get_module_snakefile(config, "hydra-genetics/parabricks", path="workflow/Snakefile", tag=config["modules"]["parabricks"])
    config:
        config


use rule pbrun_fq2bam from parabricks as parabricks_fq2bam with:
    resources:
        constraint=config.get("pbrun_fq2bam", {}).get("constraint", ""),
        gres=config.get("pbrun_fq2bam", {}).get("gres", ""),
        mem_mb=config.get("pbrun_fq2bam", {}).get("mem_mb", config["default_resources"]["mem_mb"]),
        mem_per_cpu=config.get("pbrun_fq2bam", {}).get("mem_per_cpu", config["default_resources"]["mem_per_cpu"]),
        partition=config.get("pbrun_fq2bam", {}).get("partition", config["default_resources"]["partition"]),
        threads=config.get("pbrun_fq2bam", {}).get("threads", config["default_resources"]["threads"]),
        time=config.get("pbrun_fq2bam", {}).get("time", config["default_resources"]["time"]),


use rule pbrun_deepvariant from parabricks as parabricks_deepvariant with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bai=lambda wildcards: get_bam_input(wildcards)[1],
        fasta=config.get("reference", {}).get("fasta", ""),
    output:
        vcf="parabricks/pbrun_deepvariant/{sample}_{type}.g.vcf",
        vcf2=temp("parabricks/pbrun_deepvariant/{sample}_{type}.vcf"),
    resources:
        constraint=config.get("pbrun_deepvariant", {}).get("constraint", ""),
        gres=config.get("pbrun_deepvariant", {}).get("gres", ""),
        mem_mb=config.get("pbrun_deepvariant", {}).get("mem_mb", config["default_resources"]["mem_mb"]),
        mem_per_cpu=config.get("pbrun_deepvariant", {}).get("mem_per_cpu", config["default_resources"]["mem_per_cpu"]),
        partition=config.get("pbrun_deepvariant", {}).get("partition", config["default_resources"]["partition"]),
        threads=config.get("pbrun_deepvariant", {}).get("threads", config["default_resources"]["threads"]),
        time=config.get("pbrun_deepvariant", {}).get("time", config["default_resources"]["time"]),


module prealignment:
    snakefile:
        get_module_snakefile(
            config, "hydra-genetics/prealignment", path="workflow/Snakefile", tag=config["modules"]["prealignment"]
        )
    config:
        config


use rule * from prealignment as prealignment_*


module qc:
    snakefile:
        get_module_snakefile(config, "hydra-genetics/qc", path="workflow/Snakefile", tag=config["modules"]["qc"])
    config:
        config


use rule fastqc from qc as qc_fastqc


use rule mosdepth from qc as qc_mosdepth with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bai=lambda wildcards: get_bam_input(wildcards)[1],


use rule mosdepth_bed from qc as qc_mosdepth_bed with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bai=lambda wildcards: get_bam_input(wildcards)[1],
        bed=config.get("reference", {}).get("coverage_bed", ""),
    output:
        bed=temp("qc/mosdepth_bed/{sample}_{type}.regions.bed.gz"),
        bed_csi=temp("qc/mosdepth_bed/{sample}_{type}.regions.bed.gz.csi"),
        glob=temp("qc/mosdepth_bed/{sample}_{type}.mosdepth.global.dist.txt"),
        pairbase=temp("qc/mosdepth_bed/{sample}_{type}.per-base.bed.gz"),
        region=temp("qc/mosdepth_bed/{sample}_{type}.mosdepth.region.dist.txt"),
        summary=temp("qc/mosdepth_bed/{sample}_{type}.mosdepth.summary.txt"),
        thresholds=temp("qc/mosdepth_bed/{sample}_{type}.thresholds.bed.gz"),
    params:
        thresholds="10,20,30",


use rule multiqc from qc as qc_multiqc with:
    input:
        files=lambda wildcards: set(
            [
                file.format(sample=sample, type=u.type, lane=u.lane, flowcell=u.flowcell, barcode=u.barcode, read=read, ext=ext)
                for file in config["multiqc"]["reports"][wildcards.report]["qc_files"]
                for sample in get_samples(samples)
                for u in units.loc[sample].dropna().itertuples()
                if u.type in config["multiqc"]["reports"][wildcards.report]["included_unit_types"]
                for read in ["fastq1", "fastq2"]
                for ext in config.get("picard_collect_multiple_metrics", {}).get("output_ext", [""])
            ]
        ),
        config=lambda wildcards: config["multiqc"]["reports"][wildcards.report]["config"],
        sample_order="config/sample_order.tsv",
        sample_replacement="config/sample_replacement.tsv",
    params:
        extra=lambda wildcards, input: "--replace-names "
        + input.sample_replacement
        + " --sample-names "
        + input.sample_order
        + " -c "
        + input.config,


use rule peddy from qc as qc_peddy with:
    output:
        het_check=temp("qc/peddy/peddy.het_check.csv"),
        pca=temp("qc/peddy/peddy.background_pca.json"),
        ped=temp("qc/peddy/peddy.peddy.ped"),
        ped_check=temp("qc/peddy/peddy.ped_check.csv"),
        ped_html=temp("qc/peddy/peddy.html"),
        ped_vs_html=temp("qc/peddy/peddy.vs.html"),
        sex_check="qc/peddy/peddy.sex_check.csv",


use rule picard_collect_alignment_summary_metrics from qc as qc_pic_align_sum_met with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        ref=config["reference"]["fasta"],


use rule picard_collect_duplication_metrics from qc as qc_pic_dup_met with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],


use rule picard_collect_gc_bias_metrics from qc as qc_pic_gc_met with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        ref=config["reference"]["fasta"],


use rule picard_collect_hs_metrics from qc as qc_pic_hs_met with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bait_intervals=config["reference"]["exome_intervals"],
        target_intervals=config["reference"]["exome_intervals"],
        reference=config["reference"]["fasta"],


use rule picard_collect_insert_size_metrics from qc as qc_pic_ins_size with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],


use rule picard_collect_multiple_metrics from qc as qc_pic_multi_met with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        ref=config["reference"]["fasta"],


use rule picard_collect_wgs_metrics from qc as qc_pic_wgs_met with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        ref=config["reference"]["fasta"],
        interval=config["reference"]["wgs_intervals"],


use rule samtools_stats from qc as qc_samtools_stats with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],


use rule samtools_idxstats from qc as qc_samtools_idxstats with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bai=lambda wildcards: get_bam_input(wildcards)[1],
    output:
        temp("qc/samtools_idxstats/{sample}_{type}.samtools-idxstats.txt"),


use rule verifybamid2 from qc as qc_verifybamid2 with:
    input:
        bam=lambda wildcards: get_bam_input(wildcards)[0],
        bai=lambda wildcards: get_bam_input(wildcards)[1],
        ref=config.get("reference", {}).get("fasta", ""),
        svd_mu=config.get("verifybamid2", {}).get("svd_mu", ""),


module snv_indels:
    snakefile:
        get_module_snakefile(config, "hydra-genetics/snv_indels", path="workflow/Snakefile", tag=config["modules"]["snv_indels"])
    config:
        config


use rule * from snv_indels as snv_indels_*

use rule bcftools_view from snv_indels as snv_indels_bcftools_view with:
    input:
        bcf="qc/peddy/all.bcf",
    output:
        vcf=temp("qc/peddy/all.vcf.gz"),
    log:
        "qc/peddy/all.vcf.gz.log",
    benchmark:
        repeat(
            "qc/peddy/all.vcf.gz.benchmark.tsv",
            config.get("bcftools_view", {}).get("benchmark_repeats", 1),
        )


use rule bcftools_concat from snv_indels as snv_indels_bcftools_concat_deepvariant with:
    input:
        calls=expand(
            "{{file}}_{chr}.{{vcf}}.gz", chr=extract_chr("%s.fai" % (config["reference"]["fasta"]), filter_out=skip_contigs)
        ),
        index=expand(
            "{{file}}_{chr}.{{vcf}}.gz.tbi", chr=extract_chr("%s.fai" % (config["reference"]["fasta"]), filter_out=skip_contigs)
        ),


use rule glnexus from snv_indels as snv_indels_glnexus_peddy with:
    input:
        gvcfs=lambda wildcards: get_gvcf_list(wildcards),
    output:
        bcf=temp("qc/peddy/all.bcf"),
        dir=temp(directory("qc/peddy/GLnexus_peddy.DB")),
    params:
        extra=config.get("glnexus_peddy", {}).get("extra", ""),
        glnexus_config=config.get("glnexus_peddy", {}).get("configfile", ""),
        in_gvcf=lambda wildcards, input: get_glnexus_input(wildcards, input),
    log:
        "qc/peddy/all.bcf.log",
    benchmark:
        repeat(
            "qc/peddy/all.bcf.benchmark.tsv",
            config.get("glnexus_peddy", {}).get("benchmark_repeats", 1),
        )
    container:
        config.get("glnexus_peddy", {}).get("container", config["default_container"])


use rule glnexus from snv_indels as snv_indels_glnexus_trio with:
    input:
        gvcfs=lambda wildcards: get_gvcf_trio(wildcards),
    output:
        bcf=temp("snv_indels/glnexus/{sample}_{type}.bcf"),
        dir=temp(directory("snv_indels/glnexus/{sample}_{type}/GLnexus.DB")),
    params:
        extra=config.get("glnexus_trio", {}).get("extra", ""),
        glnexus_config=config.get("glnexus_trio", {}).get("configfile", ""),
        in_gvcf=lambda wildcards, input: get_glnexus_input(wildcards, input),
    log:
        "snv_indels/glnexus/{sample}_{type}.bcf.log",
    benchmark:
        repeat(
            "snv_indels/glnexus/{sample}_{type}.bcf.benchmark.tsv",
            config.get("glnexus_trio", {}).get("benchmark_repeats", 1),
        )
    container:
        config.get("glnexus_trio", {}).get("container", config["default_container"])


use rule vt_decompose from snv_indels as snv_indels_vt_decompose with:
    input:
        vcf="snv_indels/glnexus/{sample}_{type}.vcf.gz",
    output:
        vcf=temp("snv_indels/{caller}/{sample}_{type}.decomposed.vcf.gz"),


use rule fix_af from snv_indels as snv_indels_fix_af with:
    input:
        vcf=lambda wildcards: get_vcf_input(wildcards),
    output:
        vcf=temp("vcf_final/{sample}_{type}.fix_af.vcf"),
    log:
        "vcf_final/{sample}_{type}.fix_af.vcf.log",
    benchmark:
        repeat(
            "vcf_final/{sample}_{type}.fix_af.vcf.benchmark.tsv",
            config.get("fix_af", {}).get("benchmark_repeats", 1),
        )


use rule fix_af from snv_indels as snv_indels_chrM_fix_af with:
    input:
        vcf="mitochondrial/gatk_select_variants_final/{sample}_{type}.vcf",
    output:
        vcf=temp("mitochondrial/gatk_select_variants_final/{sample}_{type}.fix_af.vcf"),
    log:
        "mitochondrial/gatk_select_variants_final/{sample}_{type}.fix_af.vcf.log",
    benchmark:
        repeat(
            "mitochondrial/gatk_select_variants_final/{sample}_{type}.fix_af.vcf.benchmark.tsv",
            config.get("fix_af", {}).get("benchmark_repeats", 1),
        )
